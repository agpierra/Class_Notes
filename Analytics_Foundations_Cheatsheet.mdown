# Analytics Foundations Cheat Sheet

![alt text](Pictures/overview_stats_models.png)

## Proc MEANS

```
proc means data = *SAS-data-set* options;
	class *variables*;
	var *variables*;
run;
```

`class` specifies variables whose values define the subgroup gombinations for the anlysis.

`var` specifies numeric variables for which you want to calculate descriptive statistics.

In `proc means` statement options:
- `clm` requests confidence limits for the mean
- `stderr` requests the standard error of the mean
- `alpha=` constructs confidence intervals with a different confidence level

### Example

```
proc means data=sasuser.testscores;
    var SATScore;
    title 'Descriptive Statistics Using PROC MEANS';
run;
```

Produces:

```
                                      The MEANS Procedure

                                 Analysis Variable : SATScore

               N            Mean         Std Dev         Minimum         Maximum
              ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ
              80         1190.63     147.0584466     890.0000000         1600.00
              ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ
```

By default, SAS prints the lines above. You can add options to means statement to request additional or alternate statistics.


```
/*st101d01.sas*/  /*Part C*/
proc means data=sasuser.testscores 
           maxdec=2 
           n mean median std q1 q3 qrange;
    var SATScore;
    title 'Selected Descriptive Statistics for SAT Scores';
run;
```

Produces:

```
                                      The MEANS Procedure

                                 Analysis Variable : SATScore

                                                          Lower          Upper       Quartile
  N           Mean         Median        Std Dev       Quartile       Quartile          Range
 ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ
 80        1190.63        1170.00         147.06        1085.00        1280.00         195.00
 ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ

```

### Exercise

Data in sasuser.NormTemp is from an article which questions the notion that the true mean body temperature is 98.6. There are 65 males and 65 females. There is also some question about whether mean body temperatures for women are the same as for men. The variables in data are:

`ID` Identification number

`BodyTemp` Body temperature

`Gender` Coded(M,F)

`HeartRate` Heart rate in bpm

Use `proc means` to answer:
- What is the overall mean and std of body temp in sample?
- what is the interquartile range of body temperature?
- Do the mean values seem to differ between men and women?
	- Hint: Use the `Class` statement in `proc means` with `gender` as the class variable

```	
proc means data=sasuser.NormTemp maxdec=2 mean std q1 q3 qrange;
	var BodyTemp;
	class gender;
run;
```

a) Overall mean is 98.25

b) Interquartile range is 0.90

c) Values somewhat differ


## Statistical Graphics Procedures in SAS

`proc sgscatter` creates single-cell and multi-cell scatter plots and scatter plot matrics with optional fits and ellipses.

`proc sgplot` creates single-cell plots with a variety of plot and chart types.

`proc sgpanel` creates single-page or multi-page panels of plots and charts conditional on classification variables.

`proc sgrender` provides a way to create plots from graph templates that you modified or wrote yourself.

## Proc UNIVARIATE

```
proc univariate data=*SAS-data-set* options;
	var *variables*;
	id *variable*;
	histogram *variables* / options;
	probplot *variables* / options;
	inset *keywords* / options;
run;
```

`var` specifies numeric variables to analyze

`id` specifies a variable used to label the five lowest and five highest values in the output

`histogram` creates high-resolution histograms

`probplot` creates a high-resolution Q-Q plot

`inset` places a box or table of summary statistics (called an inset) directly in a graph created with a `cdfplot`, `histogram`, `ppplot`, `probplot`, or `qqplot` statement. The `inset` statement **must** follow the `plot` statement that creates the plot that you want to augment.

`gpath= *file-specification* <(url=)>` specifies location for all graphics output generated while destination is open

**Selected option for** `HISTOGRAM` and `PROBPLOT` **statements:**

`normal<(options)>` creates a normal probability plot.
- options `(mu= sigma=)` determine the mean and std of the normal distribution used to create reference lines.
- curve overlays in `histogram` and diagonal reference line in `probplot`
- The est option requests that the value be estimated from the data `(mu=est sigma=est)`

**Selected option for** `HISTOGRAM` **statement:**

`kernel` superimposes kernel density estimates on the histogram

### Example

```
/*st101d02.sas*/  /*Part A*/
proc univariate data=sasuser.testscores;
    var SATScore;
    histogram SATScore / normal(mu=est sigma=est) kernel;
    inset skewness kurtosis / position=ne;
    probplot SATScore / normal(mu=est sigma=est);
    inset skewness kurtosis;
    title 'Descriptive Statistics Using PROC UNIVARIATE';
run;
```

Produces:

```
                                   The UNIVARIATE Procedure
                                      Variable:  SATScore

                                            Moments

                N                          80    Sum Weights                 80
                Mean                 1190.625    Sum Observations         95250
                Std Deviation      147.058447    Variance            21626.1867
                Skewness           0.64202018    Kurtosis            0.42409987
                Uncorrected SS      115115500    Corrected SS        1708468.75
                Coeff Variation    12.3513656    Std Error Mean      16.4416342


                                  Basic Statistical Measures

                        Location                    Variability

                    Mean     1190.625     Std Deviation          147.05845
                    Median   1170.000     Variance                   21626
                    Mode     1050.000     Range                  710.00000
                                          Interquartile Range    195.00000


                                  Tests for Location: Mu0=0

                       Test           -Statistic-    -----p Value------

                       Student's t    t  72.41525    Pr > |t|    <.0001
                       Sign           M        40    Pr >= |M|   <.0001
                       Signed Rank    S      1620    Pr >= |S|   <.0001


                                   Quantiles (Definition 5)

                                    Quantile      Estimate

                                    100% Max          1600
                                    99%               1600
                                    95%               1505
                                    90%               1375
                                    75% Q3            1280
                                    50% Median        1170
                                    25% Q1            1085
                                    10%               1020
                                    5%                 995
                                    1%                 890
                                    0% Min             890


                                     Extreme Observations

                             ----Lowest----        ----Highest---

                             Value      Obs        Value      Obs

                               890       69         1490        8
                               910       74         1520       42
                               970        6         1520       54
                               990       51         1590       70
                              1000        4         1600       25

```

- Mean of data is 1190.625. Approximately equal to median = 1170.
	- Distribution is fairly symmetric
- Standard deviation is 147.058.
	- Average variability around mean is appx. 147 points
- Distribution is slightly skewed to the right
- Distribution ahs slightly heavier tales than normal distribution

![alt text](Pictures/proc_uni_dist.png)

Bin identified with midpoint of 1100 has approximately 33% of the values.


```

                               Parameters for Normal Distribution

                                 Parameter   Symbol   Estimate

                                 Mean        Mu       1190.625
                                 Std Dev     Sigma    147.0584


                        Goodness-of-Fit Tests for Normal Distribution

                Test                  ----Statistic-----   ------p Value------

                Kolmogorov-Smirnov    D       0.08382224   Pr > D       >0.150
                Cramer-von Mises      W-Sq    0.09964577   Pr > W-Sq     0.114
                Anderson-Darling      A-Sq    0.70124822   Pr > A-Sq     0.068


                               Quantiles for Normal Distribution

                                          -------Quantile------
                                Percent    Observed   Estimated

                                    1.0     890.000     848.516
                                    5.0     995.000     948.735
                                   10.0    1020.000    1002.162
                                   25.0    1085.000    1091.436
                                   50.0    1170.000    1190.625
                                   75.0    1280.000    1289.814
                                   90.0    1375.000    1379.088
                                   95.0    1505.000    1432.515
                                   99.0    1600.000    1532.734
```

![alt text](Pictures/proc_uni_qq.png)

45-degree line represents where the data values would fall if they came from normal distribution. There does not appear to be any severe departure from normality.

We use **Kolmogorov-Smirnov** and **Anderson-Darling** to determine if the distribution is normal. Null hypothesis is that distribution is normal. **High p-values are desirable** for normality.

### Exercises

Use sasuser.NormTemp data to answer:

- What are the minimum, maximum, mean and the std for BodyTemp? Does the variable appear to be normally distributed?

```
proc univariate data=sasuser.NormTemp noprint;
	var BodyTemp;
	histogram BodyTemp / normal(mu=est sigma=est noprint) kernel;
	inset min max skewness kurtosis / position=ne;
	probplot BodyTemp / normal(mu=est sigma=est);
	inset min max skewness kurtosis;
run;
```

The distribution appears approximately normal.



## Proc SGPLOT

Procedure creates one or more plots and overlays them on a single set of axes. You can use `SGPLOT` to create statistical graphics such as histograms and regression plots, in addition to simple graphics such as box plots, scatter plots and line plots.

```
proc sgplot <*options*>;
	dot *category-variable* / options;
	hbar *category-variable* / options
	hbox *response-variable* / options
	histogram *response-variable* / options
	needle x = *variable* y = *numeric-variable* / options
	reg x = *numeric-variable* y = *numeric-variable* / options
	scatter x = *variable* y = *variable* / options
	vbar *category-variable* / options
	vbox *response-variable* / options
run;
```

`Vbox` creates a vertical box plot showing the distribution of data
	- `datalabel = option` adds data labels for **outlier markers**

`Hbox` creates a horizontal box plot showing the distribution of data

### Examples

```
/*st101d02.sas*/  /*Part B*/
proc sgplot data=sasuser.testscores;
    vbox SATScore / datalabel=IDNumber;
    format IDNumber 8.;
    refline 1200 / axis=y label;
    title "Box-and-Whisker Plots of SAT Scores";
run;
```

`refline *variable|value-1 ... value-n*` creates a horizontal or vertical reference line
	- `/ axis=` places axis on x or y

`noprint` in `proc univariate` and `histogram` statements suppresses the printing of the tabular output (often used when stats are being reported in insets of plot)


We can also use proc univariate to conduct t-tests:

```
ods graphics off;
proc univariate data=sasuser.testscores mu0=1200;
    var SATScore;
    title 'Testing Whether the Mean of SAT Scores = 1200';
run;
ods graphics on;
```

`mu0=` specifies the value of the mean or location parameter in the null hypothesis for tests of location

Outputs

```
                                 Tests for Location: Mu0=1200

                       Test           -Statistic-    -----p Value------

                       Student's t    t   -0.5702    Pr > |t|    0.5702
                       Sign           M        -5    Pr >= |M|   0.3019
                       Signed Rank    S      -207    Pr >= |S|   0.2866
```

The t-statistic and p-value are labeled Student's t and Pr > |t| respectively

We cannot reject null hypothesis at the 0.05 level in this case


### Exercises

Use sasuser.NormTemp data to answer:

b) Create box plots for BodyTemp. Use ID to identify outliers. Display a reference line at 98.6 degrees. Does the average body temperature seem to be 98.6 degrees?

```
proc sgplot data=sasuser.NormTemp;
	vbox BodyTemp / datalabel=ID;
	format ID 3.;
	refline 98.6 / axis = y label;
run;
```

The average body temperature seems to be somewhat less than 98.6 degrees as was seen in the tabular output

### Plotting scatter plots

```
/* st201d02.sas */

options formdlim="_";
title1 "Sasuser.Paper Data Set";
ods html close;
ods listing;
proc sgplot data=sasuser.paper;
 scatter x=amount y=strength;
 title2 "Scatter Plot";
run;
```



## Proc TTEST

The TTest procedure performs t-tests and computes confidence limits for one sample, paired observations, two independent sample, and the AB/BA crossover design. Can also be used to produce histograms, QQ plots, box plots, and confidence limit plots.

```
proc ttest data=*SAS-data-set*;
	class *variable*;
	paired *variables*;
	var *variables*;
run;
```

`class` specifies the two-level variable for the analysis. **Only one variable** is allowed in the `class` statement.

`paired *pairlists*` specifies the `pairlists` to identify the variables to be compaired in paired comparisons

`var` specifies *numeric* response variables for the analysis



### Example

```
proc ttest data=sasuser.testscores h0=1200
           plots(shownull)=interval;
    var SATScore;
    title 'Testing Whether the Mean of SAT Scores = 1200 '
          'Using PROC TTEST';
run;
```

`H0=` specifies the value of the mean or location parameter in the null hypothesis for tests of location (=0 by default)

`plots(shownull) = interval` includes a plot of *confidence intervals* of the mean. `shownull` places a vertical reference line at the mean value of the null hypothesis

Output:

```
                                     The TTEST Procedure

                                      Variable:  SATScore

                  N        Mean     Std Dev     Std Err     Minimum     Maximum

                 80      1190.6       147.1     16.4416       890.0      1600.0

                    Mean       95% CL Mean        Std Dev      95% CL Std Dev

                  1190.6      1157.9   1223.4       147.1       127.3    174.2

                                     DF    t Value    Pr > |t|

                                     79      -0.57      0.5702
```

Notice the 95% confidence interval around the mean includes null hypothesis value of 1200, implying a lack of statistical significance at the alpha=0.05 level.

Along with this table, we get a QQ plot and the following:

![alt text](Pictures/ttest_plots.png)

## Proc Corr

You can use corr procedure to produce correlation statistics and scatter plots for your data. By default, it produces pearson correlation statistics and corresponding p-values

```
proc corr data=*saas-data-set* options;
	var *variables*;
	with *variables*;
	id *variables*;
run;
```

In the `proc corr` line, you can use:

`plots <(only)> = ( xxxxxx )`

where xxxxx =
- ALL
- Matrix
- Scatter
- Histogram
- Nvar=all|n
- Ellipse=prediction|confidence|none

### Example

```
ods graphics / reset=all imagemap;
proc corr data=sasuser.fitness rank
          plots(only)=scatter(nvar=all ellipse=none);
    var RunTime Age Weight Run_Pulse
        Rest_Pulse Maximum_Pulse Performance;
    with Oxygen_Consumption;
    id name;
    title "Correlations and Scatter Plots with Oxygen_Consumption";
run;
```

Measures oxygen consumption vs all the `var` variables.

```
/*st103d01.sas*/  /*Part B*/
ods graphics / reset=all;
proc corr data=sasuser.fitness nosimple 
          plots=matrix(nvar=all histogram);
    var RunTime Age Weight Run_Pulse
         Rest_Pulse Maximum_Pulse Performance;
    title "Correlations and Scatter Plot Matrix of Fitness Predictors";
run;
```

Creates a matrix of plots which contains regressions of each of the `var` variables against each other. On the diagonals are histograms, as specified in the `matrix(nvar=all histogram)` line. For square matrix plot, you cannot have a `with` statement.

Low p-value -> relationship is statistically relevant. High r -> strong positive correlation. 

## Proc Reg

```
proc reg data=*SAS-data-set* options;
	model *dependents=regressors* / options
	output out=*sas-data-set* keyword=*names*;
run;
quit;
```
`output` creates a new sas data set that saves diagnostic measures calculated after fitting the model. At least one keyword=names specification is required

`plots=( )` controls plots produced through ODS graphics
- diagnotstics
- residualplot
- fitplot

**Note:** `quit` statement is required since `reg` procedure supports `run`-group processing.

### Example

```
/*st103d02.sas*/
proc reg data=sasuser.fitness;
    model Oxygen_Consumption = RunTime;
    title 'Predicting Oxygen_Consumption from RunTime';
run;
quit;
```

Results:

```
                                                        Analysis of Variance
 
                                                               Sum of           Mean
                           Source                   DF        Squares         Square    F Value    Pr > F

                           Model                     1      633.01458      633.01458      84.00    <.0001
                           Error                    29      218.53997        7.53586                     
                           Corrected Total          30      851.55455                                    


                                        Root MSE              2.74515    R-Square     0.7434
                                        Dependent Mean       47.37581    Adj R-Sq     0.7345
                                        Coeff Var             5.79442                       


                                                        Parameter Estimates
 
                                                         Parameter       Standard
                           Variable              DF       Estimate          Error    t Value    Pr > |t|

                           Intercept              1       82.42494        3.85582      21.38      <.0001
                           RunTime                1       -3.31085        0.36124      -9.17      <.0001
```


`Mean Square` is the ratio of the sum of squares and the degrees of freedom. Corresponds to the amount of variability associated with each degree of freedom for each source of variation.

`Model` is the variability explained by model (between group)

`error` is variability unexplained by model (within group)

`corrected total` is the total variability in data (Total)

F value tests whether the slope of the predictor variable is equal to 0. Small p-value (less than 0.05) means that you have enough evidence to reject the null hypothesis. Therefore, you can conclude that the simple linear regression model fits the data better than the baseline model.

`Root MSE` root mean square error is an estimate of the standard deviation of the response variable at each value of the predictor value. Square root of the MSE

`Dependent mean` is the overall mean of the response variable

`Coeff Var` is the size of the standard deviation relative to the mean. Calculated as (RootMSE/dependentmean)*100


For **paramater estimate** table:

`Parameter estimate` is the estimated beta value in the simple linear regression

`t value` is the t statistic which is calculated by dividing the parameter estimates by their corresponding standard error estimates



### Multiple linear regression

```
/*st103d04.sas*/
ods graphics off;
proc reg data=sasuser.fitness;
    model Oxygen_Consumption=Performance RunTime;
    title 'Multiple Linear Regression for Fitness Data';
run;
quit;

ods graphics on;
```

Results:

```
 
                                                               Sum of           Mean
                           Source                   DF        Squares         Square    F Value    Pr > F

                           Model                     2      646.33101      323.16550      44.09    <.0001
                           Error                    28      205.22355        7.32941                     
                           Corrected Total          30      851.55455                                    


                                        Root MSE              2.70729    R-Square     0.7590
                                        Dependent Mean       47.37581    Adj R-Sq     0.7418
                                        Coeff Var             5.71450                       


                                                        Parameter Estimates
 
                                                         Parameter       Standard
                           Variable              DF       Estimate          Error    t Value    Pr > |t|

                           Intercept              1       71.52626        8.93520       8.00      <.0001
                           Performance            1        0.06360        0.04718       1.35      0.1885
                           RunTime                1       -2.62163        0.62320      -4.21      0.0002
```
**Note:** Number of parameters in model includes the intercept

`model df` is the number of parameters minus 1 (3-1)

`error df` is the number of observations (31) minus the number of parameters in model (3)

`corrected total df` is number of observations minus 1

`f value` is the MeanSquareModel/MeanSquareError

small p-value -> reject the null hypothesis that B1 = B2 = 0 and conclude that at least one B_i ~= 0

For this case, the model can be written as:

**Oxygen_consumption** = 71.5626 + 0.06360x**Performance** - 2.62163x**RunTime**


### Model Selection

Can eliminate one variable at a time, but manually for large data sets can take an extreme amount of time.

Model Selection options
- Stepwise
- Forward
- Backward

All possible regressions ranked using
- RSquare
- AdjRSQ
- CP

Mallows' Cp
- Indicator of effective variable selection within model
- **Look for models** with `Cp <= p`, where p equals the number of parameters in the model, **including the intercept**
- Mallows recommends choosing the first (fewest variables) model where Cp approaches p

Hocking's criterion
- `Cp <= p` for prediction
- `Cp <= 2p - pfull + 1` for parameter estimation

```
/*st103d05.sas*/  /*Part A*/
ods graphics / imagemap=on;

proc reg data=sasuser.fitness plots(only)=(rsquare adjrsq cp);
    ALL_REG: model oxygen_consumption 
                    = Performance RunTime Age Weight
                      Run_Pulse Rest_Pulse Maximum_Pulse
            / selection=rsquare adjrsq cp;
    title 'Best Models Using All-Regression Option';
run;
quit;
```

`selection=` enables you to choose the different selection models. First listed is the one that determines the sorting order in output
	- RSQUARE
	- ADJRSQ
	- CP

```
/*st103d05.sas*/  /*Part B*/
ods graphics / imagemap=on;

proc reg data=sasuser.fitness plots(only)=(cp);
    ALL_REG: model oxygen_consumption 
                    = Performance RunTime Age Weight
                      Run_Pulse Rest_Pulse Maximum_Pulse
            / selection=cp rsquare adjrsq best=20;
    title 'Best Models Using All-Regression Option';
run;
quit;
```

`best= n` selects the best n models

```
/*st103d06.sas*/
ods graphics off;
proc reg data=sasuser.fitness;
   PREDICT: model Oxygen_Consumption 
                  = RunTime Age Run_Pulse Maximum_Pulse;
   EXPLAIN: model Oxygen_Consumption 
                  = RunTime Age Weight Run_Pulse Maximum_Pulse;
   title 'Check "Best" Two Candidate Models';
run;
quit;

ods graphics on;
```

Can have multiple regressions in same proc reg. Above we are testing the best 'predict' and best 'explain' (see hookings criterion)

### Stepwise selection

`Forward` selects the best one-variable model. Then it selects the best two variables among those that contain the first variable. Stops when it reaches the point where no additional variables have p-value levels less than some stopping criterion (0.50, by default)

`Backward` starts with full model. Next, the variable that is least significant, give the other variables is removed from the model. Continues this process until all of the remaining variables have p-values less than a stopping criterion value (0.10 by default)

`Stepwise` works like a combination of forward and backward. Default entry p-value is 0.15 and the default stay p-value is also 0.15.

**Caution**: Automated model selection results in:
- Biases in parameter estimates, predictions and standard errors
- Incorrect calculation of degrees of freedom
- p-values that tend to err on the side of overestimating significance (increasing type 1 error probability)

```
/*st103d07.sas*/

proc reg data=sasuser.fitness plots(only)=adjrsq;
   FORWARD:  model oxygen_consumption 
                    = Performance RunTime Age Weight
                      Run_Pulse Rest_Pulse Maximum_Pulse
            / selection=forward Slentry=0.10;
   BACKWARD: model oxygen_consumption 
                    = Performance RunTime Age Weight
                      Run_Pulse Rest_Pulse Maximum_Pulse
            / selection=backward Slstay=0.10;
   STEPWISE: model oxygen_consumption 
                    = Performance RunTime Age Weight
                      Run_Pulse Rest_Pulse Maximum_Pulse
            / selection=stepwise;
   title 'Best Models Using Stepwise Selection';
run;
quit;
```

### Regression of higher degrees

```
proc sgplot data=sasuser.paper;
    reg  x=amount y=strength / lineattrs =(color=brown       
         pattern=solid) legendlabel="Linear";
title2 "Linear Model";
run; 

proc sgplot data=sasuser.paper;
    reg  x=amount y=strength / degree=2 lineattrs =(color=green       
         pattern=mediumdash) legendlabel="2nd Degree";
title2 "Second Degree Polynomial";
run;

proc sgplot data=sasuser.paper;
   reg  x=amount y=strength / degree=3 lineattrs =(color=red   
        pattern=shortdash) legendlabel="3rd Degree";
title2 "Third Degree Polynomial";
run;

proc sgplot data=sasuser.paper;
    reg  x=amount y=strength / degree=4 lineattrs =(color=blue
      pattern=longdash) legendlabel="4th Degree";
title2 "Fourth Degree Polynomial";
run; 
```

`degree=` specifies the degree of the polynomial fit

You cannot create powers of variables within `proc reg`. We must run a data step that creates necessary items.

```
/* st201d03.sas */
title;
title2;

data paper;
   set sasuser.paper;
   amount2 = amount**2;
   amount3 = amount**3;
   amount4 = amount**4;
run;

proc reg data=paper ;
   model strength= amount amount2 amount3 amount4 / scorr1(tests);
title "Paper Data Set: 4th Degree Polynomial";
run;
quit;
```

`score1` displays the squared semi-partial correlation coefficients using type 1 sums of squares.
- `tests` requests f-tests, p-values and cumulative r-square values because variables are sequentially added to a model. Uses f-test demoninator MSE for the full model specified in model statement.
- `seqtests` uses f-test denominator MSE for  model containing all the independent variables that were added to the model up to and including variable in question 

```
                                                        Parameter Estimates
 
                                                                                   Squared
                           Parameter       Standard                           Semi-partial     Cumulative    ------Type I-----
      Variable     DF       Estimate          Error    t Value    Pr > |t|     Corr Type I       R-Square    F Value    Pr > F

      Intercept     1        3.43333        0.80170       4.28      0.0005               .              0        .       .    
      Amount        1       -1.68444        1.45927      -1.15      0.2643         0.54625        0.54625      36.11    <.0001
      amount2       1        1.02389        0.87380       1.17      0.2575         0.10748        0.65372       7.11    0.0163
      amount3       1       -0.22222        0.20982      -1.06      0.3044         0.07620        0.72992       5.04    0.0384
      amount4       1        0.01611        0.01743       0.92      0.3682         0.01293        0.74285       0.85    0.3682

```

Although it appears that none of the slopes are significantly different than 0, it is because the p values calculated are given that all the other variables are in the model.

Type I p-values are sequential. Calculate the significance of a variable given the previous variables are in the model.

Fitting a cubic model:

```
/* st201d04.sas */
proc reg data=paper plots (unpack) =(diagnostics (stats=none)); 
   Cubic_Model: model strength=amount amount2 amount3 / lackfit 
   scorr1(tests);
title "Paper Data Set: 3rd Degree Polynomial";   
run;
quit;
```

`lackfit` performs a lack of fit test. Compares the variation around the model with 'pure' variation within replicated observations. This test measures the adequacy of the specified model and can be specified only if some observations in your design are replicated

```
                                                        Parameter Estimates
 
                                                                                   Squared
                           Parameter       Standard                           Semi-partial     Cumulative    ------Type I-----
      Variable     DF       Estimate          Error    t Value    Pr > |t|     Corr Type I       R-Square    F Value    Pr > F

      Intercept     1        2.73280        0.26060      10.49      <.0001               .              0        .       .    
      Amount        1       -0.36900        0.32208      -1.15      0.2669         0.54625        0.54625      36.41    <.0001
      amount2       1        0.22339        0.11651       1.92      0.0712         0.10748        0.65372       7.16    0.0154
      amount3       1       -0.02862        0.01270      -2.25      0.0369         0.07620        0.72992       5.08    0.0369
```

Overall F-test is significant.

Lack of fit test is not significant (pvalue 0.36820), therefore there is no inadequacy of the specefied model. Type I test shows that all three slopes are significant in model.

### Multicollinearity

VIF > 10 -> presence of strong collinearity

Condition index
- Between 10 and 30 suggest weak dependencies
- Between 30 and 100 indicate moderate dependencies
- Greater than 100 indicate strong collinearity
- For large condition index, proportion of variation explained by principal components > 0.5

Using `Collin` and `collinnoint`

![alt text](Pictures/collin_stat.png)

```
/* st201d05.sas */
title 'Collinearity Diagnosis for the Cubic Model';

proc corr data=paper nosimple plots=matrix;
   var amount amount2 amount3;
run;

proc reg data=paper;
   model strength=amount amount2 amount3 / vif collin collinoint;
run;
quit;
title; 
```

`nosimple` suppresses printing simple descriptive stats for each variable

`vif` produces variance inflation factors with parameter estimates

`collin` requests a detailed analysis of collinearity among the regressors

`collinoint` requests the same analysis as `collin` with the intercept variable adjusted out rather than included in diagnostics

### Model diagnostics

```
/* st202d01.sas */
ods graphics / imagemap=on;
ods listing close;
ods html style=analysis;
proc reg data=sasuser.cars2  plots (label)=all;
   model price = hwympg hwympg2 horsepower
     / vif collin collinoint influence spec partial;
   id model;
   output out=check r=residual p=pred rstudent=rstudent h=leverage;
run;
quit;
```

`influence` requests that a detailed analysis of the influence of each observation on the estimate and the predicted values be prented

`spec` performs a test so that the first and second moments of the model are correctly specified. Null hypothesis maintains that the errors are homoscedastic, independent of the regressors, and that several technical assumptions about the model specification are valid.

`partial` requests partial regression leverage plots for each regressor

```
                                                         The REG Procedure
                                                           Model: MODEL1
                                                     Dependent Variable: Price 

                                                      Test of First and Second
                                                        Moment Specification
 
                                                     DF    Chi-Square    Pr > ChiSq

                                                      8         16.49        0.0359
                                                                                                  23

                                                         The REG Procedure
                                                           Model: MODEL1
                                                     Dependent Variable: Price 

                                                         Output Statistics
 
                                                     Hat Diag        Cov              -------------------DFBETAS------------------
      Obs            Model    Residual    RStudent          H      Ratio     DFFITS   Intercept     Hwympg    hwympg2   Horsepower

        1   Integra            -1.0276     -0.2177     0.0244     1.0774    -0.0344      0.0059    -0.0218     0.0232      -0.0151
        2   Legend              5.2465      1.1274     0.0367     1.0236     0.2199     -0.0266    -0.0837     0.0843       0.0424
        3   100                12.9697      2.8930     0.0239     0.7108     0.4527      0.0885    -0.2313     0.1656      -0.0353
        4   90                  4.3697      0.9304     0.0239     1.0317     0.1456      0.0285    -0.0744     0.0533      -0.0113
        5   535i                5.6921      1.2598     0.0882     1.0639     0.3917     -0.2901     0.3016    -0.2385       0.3528
```

In log window, there is warning due to higher ordered terms. May want to consider alternative to evaluate constant variance assumption instead of moment specification.

**Note:** null hpyothesis for chi-square test in the first and second moment specification is:
- Errors are homoscedastic
- Errors are independent of the regressors
- Several technical assumptions about the model specification are valid. For example, correct model is specified

Included in output is partial influence option output

By evaluating histogram distribution of residuals for price and Q-Q plot of residuals for price, we do not see any major problems with normality assummption

Plot of residuals versus predicted values may cause concern for the assumption of equal variances. This can be evaluated with spearman rank correlation coefficient between the absolute values of the residuals and the predicted values.

```
data check;
   set check;
   abserror=abs(residual);
run;

proc corr data=check spearman nosimple;
   var abserror pred;
run;
```

Output:

```
                                                 2  Variables:    abserror pred     


                                             Spearman Correlation Coefficients, N = 81 
                                                     Prob > |r| under H0: Rho=0
 
                                                                      abserror          pred

                                        abserror                       1.00000       0.60274
                                                                                      <.0001

                                        pred                           0.60274       1.00000
                                        Predicted Value of Price        <.0001              

Spearman rank correlation coefficient between the absolute values of residuals and predicted values is 0.603. High p-value (<0.0001) indicates a strong correlation between absolute values of residuals and predicted values. Residuals increase as predicted values increase.


To assess model fit, examine plots of residuals versus independent variables, rf plot, and the plot of observed values vs predicted.

Residual-Fit spread plot shows that model is a good fit and explains most of the variation in the data

## Proc stdize

```
proc stdize options;
	var *variables*;
run;
```

```
/* st201d06.sas */
options formdlim="_";
proc stdize data=sasuser.paper method=mean out=paper1(rename=(amount=mcamount));
   var amount;
run;

data paper1;
   set paper1;
   mcamount2 = mcamount**2;
   mcamount3 = mcamount**3;
run;                               *ST201d06.sas;      
```

`method=` specifies the name of the method for computing location and scale measures.
- `mean` `median` `std` `range`
- `method=mean` subtracts the mean of the variable for every observation.

We can also do this in SQL:

```
/***altertanively, use a SQL and a DATA step to center the variable*/
proc sql;
   select mean(amount) into: mamount
   from sasuser.paper;
run;

data paper2;
   set sasuser.paper;
   mcamount=amount-&mamount;
   mcamount2 = mcamount**2;
   mcamount3 = mcamount**3;
run;
```

When variables have a polynomial relationship with price, it might be wise to center these variables to rediuce collinearity problems with the models. You must create higher-order terms for the variables before using them in model statement of `proc reg`

```
/* st201d10.sas */
proc stdize data=sasuser.cars method=mean out=sasuser.cars2;
   var citympg hwympg fueltank weight;
run;

data sasuser.cars2;
   set sasuser.cars2;
   citympg2 = citympg*citympg;
   hwympg2 = hwympg*hwympg;
   fueltank2=fueltank*fueltank;
   fueltank3=fueltank2*fueltank;
run; 	

/* st201d11.sas */
ods graphics / reset=all;

title 'Model Selection Cars2 Data Set';
proc reg data=sasuser.cars2 plots(only) = criteria ;
   backward: 
   model price = citympg citympg2 hwympg hwympg2 cylinders
                 enginesize horsepower fueltank fueltank2 fueltank3
                 luggage weight
                 / selection=backward slstay=0.1;
   forward:
   model price = citympg citympg2 hwympg hwympg2 cylinders
                 enginesize horsepower fueltank fueltank2 fueltank3
                 luggage weight
                 / selection=forward slentry=0.1;
   Rsquared:
   model price = citympg citympg2 hwympg hwympg2 cylinders
                 enginesize horsepower fueltank fueltank2 fueltank3
                 luggage weight
                 / selection=rsquare adjrsq cp sbc aic best=3;
   plot cp.*np. / vaxis=0 to 30 by 5 haxis=0 to 12 by 1 
                  cmallows=red nostat nomodel;
   symbol v=circle w=4 h=1;
   Adjusted_R2:
   model price = citympg citympg2 hwympg hwympg2 cylinders
                 enginesize horsepower fueltank fueltank2 fueltank3
                 luggage weight 
                 / selection=adjrsq rsquare cp sbc aic best=10;
run;
quit;  
title;
```

`labelvars` requests list of regressors in the relevant model be used to label the best model at each value of the number of parameters. supported only when panel is unpacked.

`sbc` `aic` computes SBC and AIC stat for each model

`cmallows=` requests a Cp=p reference line and specifies color

`haxis=` `vaxis=` specifies tick mark values for horizontal axis / specifies range for vert axis



## Proc score

```
proc score data=*sas-data-set* score=*sas-data-set* out=*sas-data-set* other_options;
	var *variables*;
run;
```

Produces predicted values by outputting the parameter estimates from `proc reg` into a data set, and then scoring the new observations in `proc score`

### Example

```
/*st103d03.sas*/
data Need_Predictions;
    input RunTime @@;
    datalines;
9 10 11 12 13
;
run;
```
datalines above represent x values for which a prediction is wanted.

```
proc reg data=sasuser.fitness noprint outest=Betas;
    PredOxy: model Oxygen_Consumption=RunTime;
run;
quit;

proc print data=Betas;
    title "OUTEST= Data Set from PROC REG";
run;

proc score data=Need_Predictions score=Betas
           out=Scored type=parms;
    var RunTime;
run;
```

**Note:** `type=parms` is important when you run `proc score`

```
proc print data=Scored;
    title "Scored New Observations";
run;
```

proc reg p-option is another way to do this:

```
/*st103d03.sas*/  /*Self Study*/ 
data Need_Predictions;
    input RunTime @@;
    datalines;
9 10 11 12 13
;
run;

data Predict;
    set Need_Predictions 
        sasuser.fitness;
run;

ods graphics off;

proc reg data=Predict;
    model Oxygen_Consumption=RunTime / p;
    id RunTime;
    title 'Oxygen_Consumption=RunTime with Predicted Values';   
run;
quit;

ods graphics on;

```

## Proc SGScatter

Creates a paneled graph of scatter plots for multiple combinations of variables depending on the plot statement you use. Can create:
- paneled graphs of scatter plots
- panaled graphs of scatter plots with shared axes
- scatter plot matrix with prediction ellipses
- diagonal with histograms and density plots

```
proc sgscatter options;
	compare x= *variable|variable-1...variable-n*
			y= *variable|variable-1...variable-n*
			/ options;
	matrix variable-1 ... variable-n / options;
	plot *plot-requests* / options;
run;
```

`compare` creates a comparative panel of scatter plots with shared axes

`Matrix` creates a scatter plot matrix

`plot` creates a paneled graph that contains multiple independednt scatter plots

```
/* st201d01.sas */ /* Part A */
proc sgscatter data=sasuser.school;
 compare y=reading3
 	     x=(words1 letters1 phonics1);
 title 'Scatter Plots of READING3 by WORDS1 LETTERS1 and PHONICS1';
run; 								
```

Can also use `proc reg`

```
/* st201d01.sas */ /* Part B */
options formdlim="_";
proc reg data=sasuser.school 
	plots (only)=diagnostics (unpack);
   	model reading3 = words1 letters1 phonics1;
	output out=out r=residuals;
title 'School Data: Regression and Diagnostics';
run;
quit;   
```

`formdlim` specifies a character to delimit page breaks in sas output; 

`diagnostics(unpack)` produces a summary panel of fit diagnostics.
- `stats=` determines which model fit statistics are included in the model
- `unpack` produces the eight plots in the panel as individual plots

`r=residuals` creates a variable r in dataset out of the residuals in the model. We can then look at the distribution of these residuals in `proc univariate`

From running this, because the adjusted R-square for the model is 0.6904, the results of the three tests administered in the fall of the school year account for about 69% of the variability in the scores of the reading test administered in the spring.   



```
/* st201d01.sas */ /* Part C */
proc univariate data=out normal;
  var residuals;
run;
```

`normal` requests tests for normality that include a series of goodness-of-fit tests based on empirical distribution function. 

**Use Kolmogorov-Smirnov** and **Anderson-Darline**

Can examine examine the relationship of one variable with different variables:

```
/* st201d08.sas */
proc sgscatter data=sasuser.cars;
   plot price*(citympg hwympg cylinders enginesize horsepower fueltank
	    luggage weight); 
run;


ods graphics  / imagemap=on;
ods listing close;
ods html style=statistical;
proc sgscatter data=sasuser.cars;
 plot price*(citympg hwympg fueltank weight) / pbspline;
run;
ods html close;
ods listing;

```

When curvature is seen, may be useful to add a smooth curve to the plots.

`pbspline` adds a fitten, penalized B-spline curve to the scatter plot